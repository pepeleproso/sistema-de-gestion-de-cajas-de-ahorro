	IDENTIFICATION DIVISION.
	PROGRAM-ID. CajaAdmin.
	AUTHOR. GRUPO3TM.
	ENVIRONMENT DIVISION.
	CONFIGURATION SECTION.
	SOURCE-COMPUTER. PC.
	OBJECT-COMPUTER. PC.
	SPECIAL-NAMES.
				DECIMAL-POINT IS COMMA.
	INPUT-OUTPUT SECTION.
	FILE-CONTROL.
		SELECT OPTIONAL CLIENTES ASSIGN 
        TO DISK  "CLIENTES.DAT"
		ORGANIZATION IS INDEXED
		ACCESS MODE IS DYNAMIC 
		RECORD KEY IS DNI
		FILE STATUS IS SK.

		SELECT OPTIONAL CUENTAS ASSIGN
        TO DISK  "CUENTAS.DAT"
		ORGANIZATION IS INDEXED
		ACCESS MODE IS DYNAMIC 
		RECORD KEY IS NRO
		ALTERNATE RECORD KEY IS DNI-CLI
		FILE STATUS IS SK.

		SELECT OPTIONAL POLITICAS ASSIGN TO "POLITICAS.DAT"
		FILE STATUS IS SK.

		SELECT OPTIONAL OPERACIONES ASSIGN
        TO DISK  "OPERACIONES.DAT"
		ORGANIZATION IS INDEXED
		ACCESS MODE IS DYNAMIC 
		RECORD KEY IS NRO-OP
		FILE STATUS IS SK.
	
	DATA DIVISION.
	FILE SECTION.
        FD CLIENTES
		LABEL RECORD IS STANDARD
		DATA RECORD IS REG-CLIENTE.
      * El DNI se guarda en el forma: 23234567
      * El nombre y el apellido se guardan en mayusculas, para mejor comparacion.
        01 REG-CLIENTE.
        	02 DNI PIC 9(8).
			02 APELLIDO PIC A(20).
			02 NOMBRE PIC A(20).
			02 DOMICILIO PIC X(20).
			02 TELEFONO PIC X(20).
      *ESTADO DEL CLIENTE PUEDE SER:
      *"A" ->ACTIVO
      *"B" -> DADO DE BAJA
			02 ESTADO PIC A.
        FD CUENTAS
		LABEL RECORD IS STANDARD
		DATA RECORD IS REG-CUENTA.
        01 REG-CUENTA.
        	02 NRO PIC 9(8).
        	02 DNI-CLI PIC 9(8).
      * SALDO tiene formato 9999999,99
			02 SALDO PIC 9(7)v99.
      * CON FORMATO AAAAMMDD
			02 FECHA-CREACION PIC 9(8).

	FD POLITICAS DATA RECORD IS REG-POLITICAS.
	01 REG-POLITICAS.
			02 PORC-COMISION PIC 9(2).
            02 PORC-INTERES PIC 9(2).

	FD OPERACIONES DATA RECORD IS REG-OPERACIONES.
	01 REG-OPERACIONES.
			02 NRO-OP PIC 9(8).
            02 NRO-CUENTA PIC 9(8).
            02 T-OPERACION PIC 9.
            02 IMPORTE PIC 9(7)v99.
            02 CTA-ORIGEN PIC 9(8).
            02 FECHA-OP PIC 9(8).

	WORKING-STORAGE SECTION.
	01 RAYA PIC X(70) VALUE ALL "-".
	01 CORTE PIC A.
	77 SK PIC XX VALUE SPACES.
	77 opt PIC 9.
	77 optc PIC 9.
	77 busca-cli PIC 9(8).
	77 action PIC A.
	77 SALDO-ED PIC $(7)9,99.
	77 DNI-ED PIC z(8).
      * Usado como bandera para operacion de busqueda.
	77 find-code PIC A.
	77 MAX-CUENTA PIC 9(8) VALUE 99999999.
	77 NOMBRE-COMPLETO PIC A(40).
	01 FECHA-ED.
		02 ANIO PIC 9(4).
		02 MES PIC 9(2).
		02 DIA PIC 9(2).
      *full fecha tiene formato DD/MM/AAAA
	77 FULL-FECHA PIC X(10).

      * parte de declaracion de ventanas
	01 WCB.
		03 WCB-HANDLE PIC 999 BINARY VALUE 0.
		03 WCB-NUM-ROWS PIC 999 BINARY.
		03 WCB-NUM-COLS PIC 999 BINARY.
		03 WCB-LOCATION-REFERENCE PIC X.
			88 WCB-LOCATION-SCREEN-RELATIVE	VALUE "S".
			88 WCB-LOCATION-WINDOW-RELATIVE	VALUE "W".
		03 WCB-BORDER-SWITCH PIC X.
			88 WCB-BORDER-ON	VALUE "Y" WHEN FALSE "N".

		03 WCB-BORDER-TYPE PIC 9.
		03 WCB-BORDER-CHAR PIC X.
		03 WCB-FILL-SWITCH PIC X.
			88 WCB-FILL-ON VALUE "Y" WHEN FALSE "N".

		03 WCB-FILL-CHAR PIC X.
		03 WCB-TITLE-LOCATION	PIC X.
			88 WCB-TITLE-TOP		VALUE "T".
			88 WCB-TITLE-BOTTOM		VALUE "B".
		03 WCB-TITLE-POSITION	PIC X.
			88 WCB-TITLE-CENTER VALUE "C".
			88 WCB-TITLE-LEFT VALUE "L".
			88 WCB-TITLE-RIGHT VALUE "R".
		03 WCB-TITLE-LENGHT PIC 999 BINARY.
		03 WCB-TITLE PIC X(64).
        
	SCREEN SECTION.
	01 SC-DNI.
		02 FILLER  PIC 9(8)
				TO DNI LINE 5 COL 16 
                REQUIRED.

	01 SC-DATOSCLI.
		02 FILLER  PIC A(20)
				TO APELLIDO LINE 6 COL 21
                REQUIRED.
        02 FILLER  PIC A(20)
				TO NOMBRE LINE 7 COL 19
                REQUIRED.
        02 FILLER  PIC X(20)
				TO DOMICILIO LINE 8 COL 22 
                REQUIRED.
        02 FILLER  PIC X(20)
				TO TELEFONO LINE 9 COL 21.

	PROCEDURE DIVISION.
	INICIO.
    		PERFORM INICIAR-VENTANA.
		DISPLAY WCB LINE 2 COL 2 LOW ERASE
            	CONTROL "WINDOW-CREATE".
            PERFORM MENU-ADMINISTRADOR UNTIL opt = 5.
		STOP RUN.

      *###############################
      *SECCION ADMINSTRADOR
      *###############################

      *  MENU-ADMINISTRADOR
      * Muestra el menu con las acciones que puede realizar un usuario administrador
      * La opcion 5 sale del programa.
	MENU-ADMINISTRADOR.
		DISPLAY SPACES ERASE LINE 1 LOW.
		DISPLAY RAYA LINE 1 COL 2 LOW.
		DISPLAY "Bienvenido" COL 2 LOW.
		DISPLAY RAYA COL 2 LOW.
		DISPLAY "MENU:" COL 2 LOW.
		DISPLAY "1) Alta de Cliente" COL 2 LOW.
		DISPLAY "2) Activacion/Desactivacion de Cliente" COL 2 LOW.
		DISPLAY "3) Consultar Cliente" COL 2 LOW.
		DISPLAY "4) Cargar Politicas" COL 2 LOW.
		DISPLAY "5) Salir" COL 2 LOW.
		ACCEPT opt LINE 10 COL 2 LOW NO BEEP.
		EVALUATE opt 
            WHEN 1
			MOVE "A" TO CORTE
			OPEN I-O CLIENTES
			OPEN I-O CUENTAS
			PERFORM ALTA-CLIENTE UNTIL CORTE = "N"
			CLOSE CLIENTES
			CLOSE CUENTAS
			WHEN 2
			PERFORM BAJA
			WHEN 3
			PERFORM CONSULTA
			WHEN 4
			PERFORM ALTA-POLITICAS.

      *###########################################
      * ALTA-CLIENTE
      * Da de alta un nuevo cliente, se ingresan los datos y se validan
      * Si todo esta OK se graban en el archivo corresponiente y
      * se da de alta la cuenta en el archivo correpondiente
      * La carga se realiza hasta que se ingrese no.
      *###########################################
	ALTA-CLIENTE.
		DISPLAY SPACES ERASE LINE 1 COL 2 LOW.
		DISPLAY RAYA COL 2 LOW.
		DISPLAY "ALTA DE NUEVO CLIENTE" COL 2 LOW.
		DISPLAY RAYA COL 2 LOW.
      * Fuerzo el valor 0 para el DNI.
		MOVE 0 TO DNI.
		PERFORM UNTIL DNI > 0
		DISPLAY "Ingrese DNI: "  COL 3 LOW
		ACCEPT SC-DNI
		END-PERFORM.
      * Busco si ya esta cargado.
		PERFORM BUSCAR.
		IF find-code IS = "T" THEN
		DISPLAY "EL CLIENTE YA EXISTE" LINE 6 COL 3 LOW
		ELSE
		PERFORM ALTA-DAT-PERS
      * Dar de alta la nueva cuenta
		PERFORM ALTA-CUENTA
		END-IF.
		MOVE "A" TO CORTE.
		PERFORM UNTIL CORTE IS = "S" OR CORTE IS = "N"
		DISPLAY "Desea cargar otro cliente? (S/N) " LINE 22 COL 3 LOW
		ACCEPT CORTE NO BEEP LINE 22 COL 36 LOW
		END-PERFORM.
	
	ALTA-DAT-PERS.
		DISPLAY "Ingrese Apellido: " LINE 6 COL 3 LOW.
		DISPLAY "Ingrese Nombre: " LINE 7 COL 3 LOW.
		DISPLAY "Ingrese Domicilio: " LINE 8 COL 3 LOW.
		DISPLAY "Ingrese Telefono: " LINE 9 COL 3 LOW.
		ACCEPT SC-DATOSCLI.
      * Convierto a mayusculas antes de guardar para una mejor comparacion.
		INSPECT NOMBRE CONVERTING "asdfgñlkjhqwertpoiuyzxcvmnb" 
	TO "ASDFGÑLKJHQWERTPOIUYZXCVMNB"
		INSPECT APELLIDO CONVERTING "asdfgñlkjhqwertpoiuyzxcvmnb" 
	TO "ASDFGÑLKJHQWERTPOIUYZXCVMNB".
		MOVE "A" TO ESTADO.
		WRITE REG-CLIENTE.

      *###########################################
      * ALTA-CUENTA
      * Da de alta una nueva cuenta, y la asocia al cliente actual
      *###########################################
	ALTA-CUENTA.
      * BUSCO el ultimo numero de cuenta y le sumo 1.
		MOVE MAX-CUENTA TO NRO,
		START CUENTAS KEY IS LESS NRO
		INVALID KEY MOVE 0 TO NRO END-START
		IF NOT NRO IS = 0 THEN
		READ CUENTAS NEXT RECORD AT END MOVE 0 TO NRO
      	END-IF
		ADD 1 TO NRO.
		MOVE DNI TO DNI-CLI.
		MOVE 0 TO SALDO.
      * leo la fecha de creacion desde el sistema
		ACCEPT FECHA-CREACION FROM DATE.
		ACCEPT find-code.
		WRITE REG-CUENTA INVALID KEY PERFORM ERROR-CUENTA-EXISTE.
		
	BUSCAR.
      * forzar un codigo para siempre realizar la busqueda.
		MOVE "B" TO find-code.
		START CLIENTES INVALID KEY MOVE "F" TO find-code  
		NOT INVALID KEY PERFORM LEER.

	LEER.
      *  Control de errores: verificamos no procesar el EOF, si estamos en el ultimo
      * registro entonces terminamos el proceso mostrando que la busqueda no tuvo exito
		READ CLIENTES RECORD INTO REG-CLIENTE KEY IS DNI.
		MOVE "T" TO find-code.

	BAJA.
		OPEN I-O CLIENTES.
		DISPLAY SPACES ERASE LINE 1.
		DISPLAY "SISTEMA DE GESTION DE CAJA DE AHORRO".
		DISPLAY RAYA.
		DISPLAY "ACTIVACION / DESACTIVACION DE CLIENTES".
		DISPLAY RAYA.
      * Fuerzo el valor 0 para el DNI.
		MOVE 0 TO DNI.
		PERFORM UNTIL DNI > 0
		DISPLAY "Ingrese DNI: " WITH NO ADVANCING
		ACCEPT DNI NO BEEP
		END-PERFORM.
		PERFORM BUSCAR.
		IF find-code IS = "F" THEN
		PERFORM ERROR-NOT-FOUND
		ELSE
		MOVE "A" TO action
		IF ESTADO IS = "A" THEN
		PERFORM PREG-BAJA UNTIL action is = "Y" 
	or action is = "N"
		IF action IS = "Y" THEN
		MOVE "B" TO ESTADO
		REWRITE REG-CLIENTE
		DISPLAY "La operacion se realizo con exito"
		END-IF
		ELSE
		PERFORM PREG-REHABILITAR UNTIL action is = "Y" 
	or action is = "N"
		IF action IS = "Y" THEN
		MOVE "A" TO ESTADO
		REWRITE REG-CLIENTE
		DISPLAY "La operacion se realizo con exito"
		END-IF.
		CLOSE CLIENTES.

	PREG-BAJA.
		STRING NOMBRE DELIMITED BY "  "
		", " DELIMITED BY SIZE
		APELLIDO DELIMITED BY "  "
		INTO NOMBRE-COMPLETO.
		DISPLAY "¿Desea dar de baja al Cliente: " 
		NOMBRE-COMPLETO "? Y/N".
		ACCEPT action NO BEEP.

	PREG-REHABILITAR.
		STRING NOMBRE DELIMITED BY "  "
		", " DELIMITED BY SIZE
		APELLIDO DELIMITED BY "  "
		INTO NOMBRE-COMPLETO.
		DISPLAY "¿Desea rehabilitar el Cliente: " 
		NOMBRE-COMPLETO "? Y/N".
		ACCEPT action NO BEEP.
        
      * CONSULTA
      * Muestra los datos del cliente y de su cuenta
	CONSULTA.
		OPEN I-O CLIENTES.
		OPEN I-O CUENTAS.
		DISPLAY SPACES ERASE LINE 1.
		DISPLAY "SISTEMA DE GESTION DE CAJA DE AHORRO".
		DISPLAY RAYA.
		DISPLAY "CONSULTA DE CLIENTES".
		DISPLAY RAYA.
      * Fuerzo el valor 0 para el DNI.
		MOVE 0 TO DNI.
		PERFORM UNTIL DNI > 0
		DISPLAY "Ingrese DNI: " WITH NO ADVANCING
		ACCEPT DNI NO BEEP
		END-PERFORM.
		PERFORM BUSCAR.
		IF find-code IS = "F" THEN
		PERFORM ERROR-NOT-FOUND
		ELSE
		MOVE DNI TO DNI-CLI
		START CUENTAS KEY IS = DNI-CLI
		READ CUENTAS NEXT RECORD
		DISPLAY SPACES ERASE LINE 1
		DISPLAY "SISTEMA DE GESTION DE CAJA DE AHORRO"
		DISPLAY RAYA
		DISPLAY "CONSULTA DE CLIENTES"
		DISPLAY RAYA
		DISPLAY "Cliente: " WITH NO ADVANCING
		STRING NOMBRE DELIMITED BY "  "
		", " DELIMITED BY SIZE
		APELLIDO DELIMITED BY "  "
		INTO NOMBRE-COMPLETO
		DISPLAY NOMBRE-COMPLETO
		MOVE DNI TO DNI-ED
		DISPLAY "DNI: " DNI-ED
		DISPLAY "Domicilio: " DOMICILIO
		DISPLAY "Telefono: " TELEFONO
		DISPLAY SPACE
		DISPLAY "Cuenta: " NRO
      *transformo la fecha del formato AAAAMMDD
      *al formato DD/MM/AAAA
		MOVE FECHA-CREACION TO FECHA-ED
      *como no el compilador parece no devolver una fecha 
      *con anios de 4 digitos la creamos
      *esto hace que se pierdan fechas anteriores a 2000
		ADD 2000 TO ANIO
		STRING DIA DELIMITED BY SIZE
		"/" DELIMITED BY SIZE
		MES DELIMITED BY SIZE 
		"/" DELIMITED BY SIZE
		ANIO DELIMITED BY SIZE
		INTO FULL-FECHA
		DISPLAY "Fecha Alta: " FULL-FECHA
		MOVE SALDO TO SALDO-ED
		DISPLAY "Saldo Cuenta: " SALDO-ED
		DISPLAY RAYA
		ACCEPT find-code NO BEEP
		END-IF.
		CLOSE CLIENTES.
		CLOSE CUENTAS.

	ALTA-POLITICAS.
		DISPLAY SPACES ERASE LINE 1.
		DISPLAY "SISTEMA DE GESTION DE CAJA DE AHORRO".
		DISPLAY RAYA.
		DISPLAY "CARGA DE POLITICAS".
		DISPLAY RAYA.
		OPEN OUTPUT POLITICAS.
		MOVE 0 TO PORC-COMISION.
		DISPLAY "Ingrese Porcentaje de comision: " WITH NO ADVANCING.
		ACCEPT PORC-COMISION NO BEEP.
		
		MOVE 0 TO PORC-INTERES.
		DISPLAY "Ingrese Porcentaje de interes: " WITH NO ADVANCING.
		ACCEPT PORC-INTERES NO BEEP.
		WRITE REG-POLITICAS.
		CLOSE POLITICAS.

      * MENSAJES DE ERROR
	ERROR-CUENTA-EXISTE.
		DISPLAY "EL CLIENTE YA TIENE UNA CUENTA".

	ERROR-NOT-FOUND.
		DISPLAY "NO SE ENCONTRO UN CLIENTE CON DNI = " DNI.

	INICIAR-VENTANA.
		DISPLAY " " ERASE CONTROL "FCOLOR=RED,BCOLOR=WHITE".
		MOVE 23 TO WCB-NUM-ROWS.
		MOVE 75 TO WCB-NUM-COLS.
		MOVE "S" TO WCB-LOCATION-REFERENCE.
		MOVE "Y" TO  WCB-BORDER-SWITCH.
		MOVE 0 TO WCB-BORDER-TYPE.
		MOVE "*" TO WCB-BORDER-CHAR.
		MOVE "N" TO WCB-FILL-SWITCH.
		MOVE " " TO WCB-FILL-CHAR.
		MOVE "T" TO WCB-TITLE-LOCATION.
		MOVE "C" TO WCB-TITLE.
		MOVE 5 TO WCB-TITLE-LENGHT.
		MOVE "SAOCA"
            TO WCB-TITLE.